group 'nottex'
version '1.0-SNAPSHOT'

description = "nottex"

apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'utf-8'
compileTestJava.options.encoding = 'utf-8'

repositories {
    mavenCentral()
}

//noinspection GroovyAssignabilityCheck
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile('org.xhtmlrenderer:flying-saucer-pdf:9.1.5')
    antlr 'org.antlr:antlr4:4.7'

    // CSS validation
    compile group: 'com.jcabi', name: 'jcabi-w3c', version: '1.3'
    compile group: 'com.sun.jersey', name: 'jersey-client', version: '1.19.4'
    compile group: 'com.jcabi', name: 'jcabi-matchers', version: '1.4'

    // Command-line usage
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

    compile 'org.projectlombok:lombok:1.16.16'
    compile 'org.slf4j:slf4j-simple:1.7.25'

}

ext.antlr = [
        generatedDir  : 'src/gen',
        destinationDir: 'src/gen/java/grammar',
        grammarpackage: 'grammar',
        classPathDir  : 'src/gen/java'
]

generateGrammarSource {
    outputDirectory = file(antlr.destinationDir)
    arguments += ["-visitor", "-package", antlr.grammarpackage]
}

task run(type: JavaExec, dependsOn: classes) {
    main 'Main'
    classpath sourceSets.main.runtimeClasspath
}

sourceSets {
    main.java.srcDir 'src/main/java'
    main.java.srcDir 'src/gen/java'
    test.java.srcDir 'src/test/java'
}

clean {
    delete antlr.generatedDir
}

idea {
    module {
        sourceDirs += file(antlr.classPathDir)
    }
}

eclipse {
    jdt {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        file {
            File dir = file('.settings')
            dir.mkdirs()

            File f = file('.settings/org.eclipse.core.resources.prefs')
            f.write('eclipse.preferences.version=1\n')
            f.append('encoding/<project>=utf-8')
        }
    }
}

cleanEclipse.doLast {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.delete()
}

