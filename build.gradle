group 'nottex'
version '1.0-SNAPSHOT'

description = "nottex"

apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'utf-8'
compileTestJava.options.encoding = 'utf-8'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile('org.xhtmlrenderer:flying-saucer-pdf:9.0.8')
    antlr 'org.antlr:antlr4:4.5.3'
}

ext.antlr = [
        generatedDir:   'src/gen',
        destinationDir: 'src/gen/java'
]

generateGrammarSource {
    outputDirectory = file(antlr.destinationDir)
}

task run(type: JavaExec, dependsOn: classes) {
    main 'Main'
    classpath sourceSets.main.runtimeClasspath
}

sourceSets {
    main.java.srcDir 'src/main/java'
    main.java.srcDir antlr.destinationDir
    test.java.srcDir 'src/test/java'
}

clean {
    delete antlr.generatedDir
}

idea {
    module {
        sourceDirs += file(antlr.destinationDir)
    }
}

eclipse {
    jdt {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        file {
            File dir = file('.settings')
            dir.mkdirs()

            File f = file('.settings/org.eclipse.core.resources.prefs')
            f.write('eclipse.preferences.version=1\n')
            f.append('encoding/<project>=utf-8')
        }
    }
}

cleanEclipse << {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.delete()
}

